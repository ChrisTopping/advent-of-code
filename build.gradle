plugins {
    id 'java'
//    id 'jacoco'
//    id 'org.barfuin.gradle.jacocolog' version '1.0.1'
    id 'application'
}

group = 'com.cpt'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.pkg.github.com/ChrisTopping/tensor'
        credentials {
            username = System.getenv("PACKAGES_USER")
            password = System.getenv("PACKAGES_TOKEN")
        }
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/it/java')
        }
    }
}

configurations {
    compileOnly {
        extendsFrom(configurations.annotationProcessor)
    }
    integrationTestImplementation {
        extendsFrom(configurations.testImplementation)
    }
    integrationTestRuntimeOnly {
        extendsFrom(configurations.testRuntimeOnly)
    }
}

dependencies {
    implementation 'dev.christopping:tensor:1.0.9'

    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.projectlombok:lombok:1.18.40'
    compileOnly 'org.projectlombok:lombok:1.18.40'
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'com.diogonunes:JColor:5.2.0'
    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation 'org.slf4j:slf4j-nop:1.8.0-beta4'
    implementation 'com.squareup.okhttp3:okhttp:5.3.0'
    implementation 'org.jsoup:jsoup:1.17.1'
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.40'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testCompileOnly 'org.projectlombok:lombok:1.18.40'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.9.3'
}

test {
    useJUnitPlatform()
//    finalizedBy jacocoTestReport
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Run integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter(tasks.test)
}

check.dependsOn integrationTest

//jacoco {
//    toolVersion = "0.8.7"
//}

//jacocoTestReport {
//    dependsOn test
//    reports {
//        xml.required = true
//        csv.required = false
//        html.outputLocation = layout.buildDirectory.dir('jacoco')
//    }
//}

application {
    mainClass = 'com.cpt.adventofcode.SolverRunner'
    description = 'Advent of Code Solver'
    applicationDefaultJvmArgs = []
}

tasks.register('generate', JavaExec) {
    group = 'Execution'
    description = 'Generate a new solution'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.cpt.adventofcode.GeneratorRunner'
    standardInput = System.in
    standardOutput = System.out
}

tasks.register('scrape', JavaExec) {
    group = 'Execution'
    description = 'Generate a new solution by scraping adventofcode'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.cpt.adventofcode.ScraperRunner'
    standardInput = System.in
    standardOutput = System.out
}